{
  depot,
  pkgs,
  lib,
  ...
}:

(depot.snix.crates.workspaceMembers.snix-store.build.override (old: {
  runTests = true;
  testPreRun = ''
    export SSL_CERT_FILE=${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt
  '';
  features =
    old.features
    # virtiofs feature currently fails to build on Darwin
    ++ lib.optional pkgs.stdenv.isLinux "virtiofs";
})).overrideAttrs
  (old: rec {
    meta.ci = {
      targets = [
        "integration-tests"
      ]
      ++ lib.filter (x: lib.hasPrefix "with-features" x || x == "no-features") (lib.attrNames passthru);
    };
    passthru =
      old.passthru
      // (depot.snix.utils.mkFeaturePowerset {
        inherit (old) crateName;
        features = [
          "cloud"
          "fs"
          "otlp"
          "xp-composition-cli"
        ];
        override.testPreRun = ''
          export SSL_CERT_FILE=${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt
        '';
      })
      // {
        integration-tests = depot.snix.crates.workspaceMembers.${old.crateName}.build.override (old: {
          runTests = true;
          testPreRun = ''
            export SSL_CERT_FILE=${pkgs.cacert}/etc/ssl/certs/ca-bundle.crt
            export PATH="$PATH:${
              pkgs.lib.makeBinPath [
                pkgs.cbtemulator
                pkgs.google-cloud-bigtable-tool
              ]
            }"
          '';
          features = old.features ++ [ "integration" ];
        });
      };
  })
